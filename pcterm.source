0 GRAPHIC1:IFPEEK(4906)=67THENGRAPHIC0:PRINT"{clear}sys4867:run1":POKE842,ASC("{home}"):POKE843,13:POKE208,2:END
1 FAST:GRAPHIC5:PRINTCHR$(27)"e"CHR$(14)CHR$(11);:GRAPHIC1:POKE51,0:POKE52,127:POKE57,0:POKE58,255:CLR:COLOR0,1:COLOR4,1:cl%=2:GRAPHIC5:WINDOW0,0,79,24
2 COLOR5,2:pa$(0)="None":pa$(1)="Odd":pa$(2)="Even":pa$(3)="Mark":pa$(4)="Space":GRAPHIC5
3 wl$(0)="8 Btts":wl$(1)="7 Bits":wl$(2)="6 Bits":wl$(3)="5 Bits":ee$(0)="Yes":ee$(1)="No"
4 sb$(0)="1 Bit":sb$(1)="2 Bits":hs$(0)="3 Line":hs$(1)="X Line"
5 tb%=28:ln%=78:GRAPHIC0:GRAPHIC5
6 cr$=CHR$(13)
8 CHAR1,0,0,"{cm a}{sh asterisk*33}<(PC-Term128)>{sh asterisk*31}{cm s}":PRINTCHR$(27)"m";
9 FORt=1TO20:CHAR1,0,t,"{sh -}{space*78}{sh -}":NEXT
10 CHAR1,0,21,"{cm q}{sh asterisk*39}{cm r}{sh asterisk*38}{cm w}"
11 CHAR1,0,22,"{sh -}{space*4}(C)1987 PC-Soft by Sam Pullara{space*5}{sh -}{space*38}{sh -}"
12 CHAR1,0,23,"{cm z}{sh asterisk*39}{cm e}{sh asterisk*38}{cm x}"
13 CHAR1,27,24,"Master Menus(tm) by PC-Soft":PRINTCHR$(27)"l";
14 wa=DEC("e{0*3}"):bs=255:cs=1022727:xc=8429
15 t$(1)=" Program ":t$(2)=" Seq ":t$(3)=" User ":ty$="psu"
16 DIM a$(150),h$(40),h1$(40),h1(40),ni$(20),ni(20),nn$(20),p1$(40),p2$(40),p3$(40),z1$(20),z2$(20),z3$(20),m$(15),cq$(16)
17 GRAPHIC5
18 BANK 15:GRAPHIC5
19 xm=0:tb$=CHR$(7)
21 REM mu2034 md2142 u2002 d2042
26 DIMml$(20),md$(20),ed$(100),c1$(100):cr$=CHR$(13):dv=1:GOSUB4170:tt%=300:bu$(0)="Closed":bu$(1)="Open"
27 a$="":b$="":a=0:b=0:tm$=""
28 POKE208,0:POKE3071,0:POKE251,128:POKE250,0:POKE3070,0:POKE3068,0:POKE3069,0:BANK1:POKE32768,0:BANK15
29 bt=pc+2072:ft=pc+2075:sl=4994:sh=4995:fr=xc+883:DIMco$(50),aa$(40),an$(40),pd(40),a1$(40),a2(40)
30 dp$(0)="Full":dp$(1)="Half":cm$(0)="Ascii":cm$(1)="Comscii":p$(0)="t":p$(1)="p"
35 ph$(0)="Tone":ph$(1)="Pulse":mo$(0)="1660":mo$(1)="1650":mo$(2)="1670":pu$(0)="Lightning":pu$(1)="Fast":pu$(2)="Normal":pu$(3)="Slow":
40 OPEN5,2,0,CHR$(8)+CHR$(0):GOSUB700:GET#5,a$:SYSDEC("20ba"):mo$(3)="Mitey Mo":au$(0)="No":au$(1)="Yes"
45 bs=255
50 xm$(0)="Punter":xm$(1)="Xmodem"
56 rp$(0)="Verbose":rp$(1)="Terse"
60 IFmo=2THENbr$="1200":ELSEbr$="300"
65 GOSUB1900
70 PRINTCHR$(27)"s"CHR$(27)"e";:DIMdt$(200)
75 BANK15
80 a=54784:BANK15:POKEa,12:POKEa+1,16:POKEa,13:POKEa+1,0:POKE2606,16:WINDOW0,0,79,24,1
85 POKEa,12:POKEa+1,0:POKEa,13:POKEa+1,0:POKE2606,0
90 GOSUB1800:TEMPO20:PLAY"u8x0t9io4dgbqo5dio4bo5qdm"
92 BANK15
95 COLOR5,c2:t=0:PRINTCHR$(14)CHR$(27)"s";:mn$="MainMenu"
100 BANK15:CLOSE1:co$(1)="Wait for Call":co$(2)="Call a Number":co$(3)="Hang-Up":co$(4)="Pick-Up"
105 co$(5)="Terminal Mode":co$(6)="Direct Connect":co$(7)="Disk Options":co$(8)="Wardialer":co$(9)="Baud:"+br$
110 co$(10)="Download:"+xm$(xm):co$(11)="Upload:"+xm$(xm):co$(12)="Toggle Protocol"
115 co$(13)="Phonebook":co$(14)="Redial":co$(15)="Macro-Mode"
120 co$(16)="Configure Menu":IFxm=0THENco$(17)="Multi-Transfer"
129 f1=0
130 cn=0:co=17-xm
140 GOSUB26000
150 wa=0:BANK15:ONaGOSUB160,500,435,595,300,180,4000,1000,1930,2042,2002,1705,3000,1740,1500,3600,9000
155 GOTO95
160 wa=1:WINDOW1,1,78,2,1:CHAR1,34,0,"Waiting{.*3}":WINDOW1,2,78,20
165 PRINT"{clear}Waiting for call."
170 IF(PEEK(56577)AND8)=0THENPRINT"Ringing{.*3}{ct g}";:SLEEP1:GOTO180
175 GETa$:IFa$<>""THENRETURN:ELSE:GOTO170
180 WINDOW1,1,78,2,1:CHAR1,33,0,"Direct Connect":WINDOW1,2,78,19,1:BANK15:tt=0
185 tt=0:IFmo=1THENBANK15:POKE56579,0
190 PRINT"{clear}Waiting for Carrier.";
200 IF((PEEK(56577)AND16)=16)THENtt=tt+1:IFtt=INT(tt%*1.6)ANDb$<>"e"ANDb$<>"a"THENPRINT"{clear}No Answer.";:GOSUB435:SLEEP1:GOTO510:tt=0
205 GETa$:IFa$<>""THENPRINT"{clear}Aborted.";:SLEEP1:GOSUB435:RETURN
215 IF((PEEK(56577)AND16)=0)THENPRINT"{clear}{ct g}Carrier detected.";:GOSUB1295:GOTO300
220 PRINT"{up}";:GOTO200
300 IFmc%=1THENmc%=0:RETURN
302 IFwa=1THENSLEEP1:PRINT"{clear}[ Sending Buffer ]":GOSUB3933:wa=0
305 WINDOW1,1,78,20,1:GOTO1950
435 ho=0:IFmo=1THEN445
440 IFmo=2THEN455
445 BANK15:POKE56577,(PEEK(56577)OR32):BANK0:RETURN
450 BANK15:POKE56579,32:POKE56577,0:BANK0:RETURN
455 PRINT#5,"{+*3}";:SLEEP1:RETURN
500 WINDOW1,1,78,2,1:CHAR1,34,0,"CallNumber":WINDOW20,2,78,20,1
505 nm$="":PRINT"{clear}Phone #:";:GOSUB3490:nm$=en$:PRINT"{clear}":IFnm$=""THENRETURN
510 WINDOW1,cl%,78,20,1:CHAR1,0,20-cl%,"Dialing{.*3}"+CHR$(15)+nm$+CHR$(143)
515 IFph=1ANDmo<>2THEN3235
520 IFmo<>2THENGOSUB595:SLEEP1:t=0
525 IFmo=2THENBEGIN:PRINT#5,"atd"+p$(ph)+nm$+cr$:IFf1=0THENGOTO180:ELSERETURN:BEND
530 sid=54272:POKEsid+24,15:POKEsid+4,16:POKEsid+11,16:POKEsid+5,0:POKEsid+12,0:POKEsid+6,15*16:POKEsid+13,15*16:POKEsid+23,0
535 FORg=1TOLEN(nm$):a=VAL(MID$(nm$,g,1))
540 IFa>0ANDa<4THENPOKEsid+7,168:POKEsid+8,44
545 IFa>3ANDa<7THENPOKEsid+7,85:POKEsid+8,49
550 IFa>6ANDa<10THENPOKEsid+7,150:POKEsid+8,54
555 IFa=0THENPOKEsid+7,74:POKEsid+8,60
560 IFa=1ORa=4ORa=7THENPOKEsid,117:POKEsid+1,77
565 IFa=2ORa=5ORa=8ORa=0THENPOKEsid,152:POKEsid+1,85
570 IFa=3ORa=6ORa=9THENPOKEsid,161:POKEsid+1,94
575 POKEsid+4,17:POKEsid+11,17:FORt=1TO50:NEXT
580 POKEsid+4,16:POKEsid+11,16:FORt=1TO50:NEXT
585 NEXT:IFf1=0THENGOTO180
590 RETURN
595 ho=1:IFmo=1THEN620
600 IFmo=2THEN625
605 BANK15:oh=56577:hi=32:lo=255-32:IFmo=3THENPOKE56577,2
610 POKE(oh+2),(PEEK(oh+2)ORhi)
615 POKEoh,(PEEK(oh)ANDlo):BANK15:RETURN
620 BANK15:POKE56579,32:POKE56577,32:BANK15:RETURN
625 PRINT#5,"atdt"+cr$:RETURN
700 FORt=1TO8:KEYt,CHR$(132+t):NEXT
705 GOSUB3675:IFmo=2THENPRINT#5,"atm0s0=0s7=255s2=43v1x1e1"+cr$:SLEEP2
706 GOSUB5500
709 tx$="Loading Phonebook{.*3}":GOSUB1335
710 hh=1:OPEN2,dv%(dv),2,pn$+",s,r":IFst=0THENDO:INPUT#2,aa$(hh),an$(hh),a1$(hh),a2(hh):hh=hh+1:LOOPUNTILst<>0:hh=hh-1:DCLOSE#2:ELSEDCLOSE#2:DCLEAR
715 BANK15:IFPEEK(9000)<>6THENtx$="Loading PC.ML{.*3}":GOSUB1335:BLOAD"pc.ml",b0,u(dv%(dv))
716 BANK15:IFPEEK(9000)<>6THENtx$="Loading PC.ML6{.*3}":GOSUB1335:BLOAD"pc.ml6",b0,p8192:u(dv%(dv)):SYSDEC("2f00"):SYSDEC("2{0*3}")
720 IFPEEK(5133)<>13THENtx$="Loading Ascii Tables{.*3}":GOSUB1335:BLOAD"pc.ascii",p5120,u(dv%(dv))
725 GOSUB4090
727 IFPEEK(2816)<>32THENtx$="Loading PC.ML2{.*3}":GOSUB1335:BLOAD"pc.ml2",p2816,b0,u(dv%(dv))
728 IFPEEK(3584)<>76THENtx$="Loading PC.ML3{.*3}":GOSUB1335:BLOAD"pc.ml3",p3584,b0,u(dv%(dv))
730 RETURN
735 BANK15:WINDOW0,0,79,24,1
740 KEY1,"gR":KEY2,"dload"+CHR$(34):KEY3,"diR"+cr$:KEY4,"scrclr"+cr$:KEY5,"dS"+CHR$(34):KEY6,"run"+CHR$(13):KEY7,"list":KEY8,"moN"+cr$
745 BANK15:END
750 yy$="":OPEN1,dv%(dv),0,"$0:"+dr$+"*":PRINT:PRINT#5:a$="{space*21}":dr=LEN(dr$):IFdr>1THEN760
755 tx$="BLK FILENAME{space*10}TYP"+cr$+"{-*3} {-*17} {-*3}":GOSUB1150:GOTO765
760 tx$="FILES"+cr$+"{-*5}":GOSUB1150:fi%=0
765 GET#1,x$,y$
770 GET#1,x$,y$
775 GET#1,x$,y$:c=0
780 IFx$<>""THENc=ASC(x$)
785 IFy$<>""THENc=c+ASC(y$)*256
790 IFc=0ORdr>1THEN810
795 PRINTMID$(STR$(c),2)" ";:IFc<10THENPRINT" ";
796 IFc<100THENPRINT" ";
800 PRINT#5,MID$(STR$(c),2)" ";:IFc<10THENPRINT#5," ";
805 IFc<100THENPRINT#5," ";
810 GET#1,y$:IFst<>0THEN875
815 IFy$<>CHR$(34)THEN810
820 GET#1,y$:IFy$<>CHR$(34)THENyy$=yy$+y$:GOTO820
825 GET#1,y$:IFy$=CHR$(32)THEN825
830 IFc=0THEN845
835 IFdr$=""THENtx$=yy$:GOSUB1145:ELSE:tx$=RIGHT$(yy$,LEN(yy$)-LEN(dr$)):GOSUB1145
840 fi%=fi%+1:yy=16-LEN(yy$):PRINTRIGHT$(a$,yy);:PRINT#5,RIGHT$(a$,yy);:z$="":yy$=""
845 z$=z$+y$:GET#1,y$:IFy$<>""THEN845
850 IFc=0ORdr>1THEN860
855 PRINT"{space*2}"LEFT$(z$,3):PRINT#5,"{space*2}"LEFT$(z$,3)
860 GETtt$:GETt$:IFt$<>""ORtt$<>""THENGOTO885
865 IFdr>1THENPRINT:PRINT#5
870 z$="":yy$="":y$="":c=0:IFst=0THEN770
875 IFdr<2THENtx$="Blocks Free"+cr$+STR$(fi%)+" Files"+cr$+STR$(c*256)+" Bytes Free":GOSUB1150:ELSEPRINT
880 CLOSE1:RETURN
885 IFt$=CHR$(27)ORa$=CHR$(27)THENCLOSE1:RETURN
890 GETt$:GET#5,a$:IFt$<>""THEN885
895 PRINTds$:PRINT#5,ds$:DCLOSE:RETURN
900 WINDOW1,1,78,2,1:CHAR1,33,0,"Disk Command":WINDOW1,2,78,20,1
905 cd=2:PRINT"{clear}Disk Command:":PRINT">";:en$="":GOSUB3490:b$=en$:IFb$=""THENRETURN:ELSE:OPEN6,8,15,b$:GOSUB920:CLOSE6
910 cd=0:RETURN
915 PRINTds$:RETURN
920 PRINT:PRINT:PRINT"Status:"ds$:GETKEYa$:RETURN
1000 WINDOW1,1,78,2,1:CHAR1,34,0,"War-Dialer":WINDOW1,2,78,20,1:bb=0
1005 f1=1:sn=0:en=0:pr$="":PRINT"{clear}Wardialer":PRINT"Area Code({x*3}):";:GOSUB3490:pr$=en$:PRINT"Start #:";:GOSUB3490:sn=VAL(en$):IFpr$<>""THENpr$="1"+pr$
1010 PRINT"{space*2}End #:";:GOSUB3490:en=VAL(en$):IFsn=0ORen=0THENRETURN
1015 FORgg=snTOen:nm$=pr$+RIGHT$(STR$(gg),7):GOSUB435:SLEEP2:tx$="Carriers:"+STR$(bb):GOSUB1335:GOSUB510
1020 GETa$:IFa$=CHR$(27)THENgg=en:NEXT:GOTO1030
1025 tt=0:GOSUB1045:NEXT
1030 IFbb>0THENPRINT"{clear}Numbers":FORt=0TObb-1:PRINTdt(t):NEXT:ELSEPRINT"{clear}";
1035 GOSUB435:PRINT"Done!":GETKEYzz$:RETURN
1040 tt=tt+1:IFmo=1THENBANK15:POKE56579,0
1045 GETa$:IF((PEEK(56577)AND16)<>0)ANDtt<>tt%ANDa$=""THEN1040
1050 IFtt=tt%THENRETURN
1055 IFa$=CHR$(27)THENgg=en:RETURN
1060 SLEEP1:IF((PEEK(56577)AND16)<>0)THEN1040
1065 dt(bb)=gg:bb=bb+1:GOSUB1295:GOSUB435:SLEEP1:RETURN
1145 PRINTtx$:PRINT#5,tx$:RETURN
1150 PRINTtx$;:PRINT#5,tx$;:RETURN
1165 x=PEEK(236):y=PEEK(235)
1166 COLOR5,c2:WINDOW1,2,78,2,1:a$="Duplex:"+dp$(dp)+"{space*2}Mode:"+cm$(cm)+"{space*2}Baud:"+br$+"{space*2}Buffer:"+bu$(b3)
1167 CHAR1,(78-LEN(a$))/2,0,a$:WINDOW1,3,78,20:CHAR1,x-1,y-3,"":RETURN
1170 a=54784:BANK15:POKEa,12:POKEa+1,16:POKEa,13:POKEa+1,0:POKE2606,16:x=PEEK(236):y=PEEK(235)
1172 COLOR5,c2:WINDOW0,0,79,24,1:WINDOW9,3,78,20
1175 PRINT"{clear}{cm a}{sh asterisk*57}{cm s}":FORt=1TO14:PRINT"{sh -}{space*57}{sh -}":NEXT
1180 PRINT"{cm z}{sh asterisk*57}{cm x}"
1185 WINDOW10,4,78,20
1186 PRINT"{space*9}Help (All functions used with Alt key):"
1190 PRINT"[1] Duplex"TAB(20)"[U] Upload"TAB(40)"[D] Download"
1195 PRINT"[2] Ascii/Comscii"TAB(20)"[O] Open Buffer"TAB(40)"[C] Close buffer"
1200 PRINT"[3] Hex to Decimal"TAB(20)"[Q] Buffer Clear"TAB(40)"[B] Buffer Bytes"
1205 PRINT"[4] Decimal to Hex"TAB(20)"[P] Print Buffer"TAB(40)"[F1-F8] F-Keys*"
1210 PRINT"[5] Seq Reader"TAB(20)"[S] Save Buffer"TAB(40)"[L] Load Buffer"
1215 PRINT"[6] Change Baud"TAB(20)"[+] Pick-Up"TAB(40)"[-] Hang-Up"
1220 PRINT"[7] Slow Dir"TAB(20)"[A] AutoDial"TAB(40)"[T] Transfer Time"
1225 PRINT"[8] Fast Dir"TAB(20)"[Home] Menu*"TAB(40)"[N] Autodial (L)"
1230 PRINT"[H] AT Command"TAB(20)"[@] Disk Command"TAB(40)"[?] Help"
1231 PRINT"[R] ReRead Peripheral Status"TAB(40)"[E] The Editor"
1232 PRINT"[M] Protocol"TAB(20)"[*] Redial"TAB(40)"[{pound}] Print Memo"
1233 PRINT"[HOME] Clear Screen"TAB(20)"[Alt-Shift-Letter] Control Char"
1234 PRINTTAB(17)"(* = Alt key not used){up}"
1235 GETKEYa$:WINDOW1,3,78,20:POKEa,12:POKEa+1,0:POKEa,13:POKEa+1,0:POKE2606,0:CHAR1,x-1,y-3,"":RETURN
1260 RETURN
1270 PRINT"{clear}Are you sure?":GETKEYa$:IFa$<>"y"THENEND:ELSEPRINT"{clear}Saving{.*3}"
1275 SCRATCH"{sh -}pcterm.bak{space*4}{sh -}":RENAME"{sh -}pcterm{space*8}{sh -}"TO"{sh -}pcterm.bak{space*4}{sh -}":DSAVE"{sh -}pcterm{space*8}{sh -}":END
1295 TEMPO20:PLAY"u8x0t9io4dgbqo5dio4bo5qdm":FORtt=1TO10:PRINTCHR$(27)"r"CHR$(27)"n";:NEXT:RETURN
1300 PRINT"{clear}Background:";:GOSUB3490:IFen$=""THENen$="1"
1305 c1=VAL(en$):IFc1=0THENc1=1
1310 PRINT"Foreground:";:GOSUB3490:IFen$=""THENen$="2"
1315 c2=VAL(en$):IFc2=0THENc2=2
1320 COLOR6,c1:COLOR5,c2
1321 a=54784:BANK15:POKEa,12:POKEa+1,16:POKEa,13:POKEa+1,0:POKE2606,16:WINDOW0,0,79,24,1
1322 POKEa,12:POKEa+1,0:POKEa,13:POKEa+1,0:POKE2606,0:GOTO3600
1325 tx$=ds$:GOSUB1335:RETURN
1330 x=PEEK(236):y=PEEK(235):cc=(37-LEN(tx$))/2
1331 PRINT"{home}";:WINDOW1,0,78,24:CHAR1,41,22,"{space*36}":CHAR1,41+cc,22,(tx$):WINDOW1,3,78,20
1332 CHAR1,x-1,y-3,"":RETURN
1335 cc=(37-LEN(tx$))/2
1336 PRINT"{home}{reverse off}";:WINDOW1,0,78,24:CHAR1,41,22,"{space*36}":CHAR1,41+cc,22,(tx$):RETURN
1340 PRINT"{clear}":WINDOW27,2,78,20,1
1490 FORt=0TObu:bu$(t)="":NEXT:bu=0:GOTO1500
1500 CLOSE1:PRINT"{clear}";:mn$="Macro-Mode"
1505 co$(1)="Load Macro":co$(2)="Save Macro":co$(3)="Create Macro":co$(4)="Destroy Macro":co$(5)="Execute Macro"
1510 co$(6)="List Macros":co$(7)="Append Macro":co$(8)="Delete Macro":co$(9)="See Buffer":co$(10)="Clear Buffer":co$(11)="Main Menu"
1515 co=11:GOSUB26000
1520 ONaGOTO1545,1555,1565,1700,1605,1745,1750,1535,2455,1490,1530
1525 GOTO1515
1530 GOSUB1260:RETURN
1535 PRINT"{clear}Enter Name Of Macro:";:GOSUB3490:IFen$=""THEN1500
1540 SCRATCH"mcr."+en$:PRINTds$:GETKEYa$:GOTO1500
1545 PRINT"{clear}Filename:";:en$="A*":GOSUB3490:f$=en$:OPEN1,dv%(dv),2,"0:mcr."+f$:IFds<>0THENDCLOSE:GOTO1500
1550 PRINT"{clear}Loading{.*3}"f$:t1=-1:DO:t1=t1+1:INPUT#1,c1$(t1):s=st:PRINTc1$(t1):LOOPUNTILs<>0:DCLOSE:PRINTds$:GETKEYa$:GOTO1500
1555 PRINT"{clear}Filename:";:en$="":GOSUB3490:f$=en$:SCRATCH("mcr."+f$):OPEN1,dv%(dv),2,"0:mcr."+f$+",p,w":IFds<>0ORen$=""THENDCLOSE:GOTO1500
1560 PRINT"{clear}Saving{.*3}"f$:FORtt=0TOt1:PRINT#1,CHR$(34)+c1$(tt):PRINTc1$(tt):NEXT:DCLOSE:PRINTds$:GETKEYa$:GOTO1500
1565 WINDOW1,1,78,2,1:CHAR1,34,0,"CreateMode":WINDOW1,2,78,20,1
1566 FORt=0TOt1:ed$(t)=c1$(t):NEXT:ed=t1:f$="mcr.macro"
1570 GOSUB6500
1575 FORt=0TOed:c1$(t)=ed$(t):NEXT:t1=ed:GOTO1500
1605 WINDOW1,1,78,2,1:CHAR1,34,0,"Executing{.*3}":WINDOW1,2,78,20,1
1610 FORt2=0TOt1:a$=LEFT$(c1$(t2),2):tx$=c1$(t2):GOSUB1335:WINDOW1,2,78,20
1615 IFa$="c-"THENnm$=RIGHT$(c1$(t2),LEN(c1$(t2))-2):tr=2:mc%=1:GOSUB510:tr=0:WINDOW1,2,78,20,1
1620 IFa$="p-"THENBEGIN:tx$="":aa$="":c1$(t2)=RIGHT$(c1$(t2),LEN(c1$(t2))-2)
1621 IFRIGHT$(c1$(t2),1)="{arrow left}"THENtx$=LEFT$(c1$(t2),LEN(c1$(t2))-1)+cr$:GOTO1630
1625 tx$=c1$(t2)
1630 GOSUB2685:PRINT#5,ou$;:BEND
1635 IFa$="xx"THENGOSUB435:t2=t1:NEXT:GOTO1500
1640 IFc1$(t2)="bo"THENbo=1:NEXT
1645 IFc1$(t2)="bc"THENbo=0:NEXT
1650 IFa$="d-"THENsa=0:t=1:i$=RIGHT$(c1$(t2),LEN(c1$(t2))-2):GOSUB2095
1655 IFa$="u-"THENf5=1:t=1:i$=RIGHT$(c1$(t2),LEN(c1$(t2))-2):mc%=1:GOSUB2045:f5=0:mc%=0
1660 IFa$="b-"THENbr$=RIGHT$(c1$(t2),LEN(c1$(t2))-2):GOSUB1900
1665 IFa$="s-"THENBEGIN:s$=RIGHT$(c1$(t2),6)
1670 DO:GETa$:LOOPUNTILti$=s$ORa$<>"":IFa$<>""THENt2=t1:NEXT:GOTO1500:ELSENEXT:BEND
1675 GET#5,a$:IFa$<>""ANDbo=1THENGOSUB1690
1680 IFa$=""THENaa=aa+1:ELSEaa=0:GOTO1675
1681 GETa$:IFa$<>""THENa$="xx":GOTO1635
1685 IFaa=300THENaa=0:NEXT:ELSE:GOTO1675
1690 bu$(bu)=bu$(bu)+a$:IFLEN(bu$(bu))=250THENbu=bu+1
1695 RETURN
1700 FORtt=0TOt1:c1$(tt)="":NEXT:t1=0:GOTO1500
1705 xm=-xm+1:IFxm=1THENbs=128:RETURN:ELSEbs=255:RETURN
1710 ph=-ph+1:GOTO3600
1715 PRINT"{clear}";:WINDOW1,1,78,2,1:CHAR1,34,0,"PhoneBook:"
1720 WINDOW1,2,78,20,1:FORt=1TOhh:PRINTCHR$(t+96)". "aa$(t)TAB(20)":"an$(t):NEXT:PRINTCHR$(t+96)". Quit":PRINT"Which One?"
1725 GETKEYa$:a=ASC(a$)-64:IFa<1ORa>26THEN1725
1730 IFa=tTHENRETURN
1735 nm$=an$(a):GOTO510
1740 PRINT"{clear}Dialing{.*3}"nm$:IFnm$<>""THENGOSUB510:RETURN:ELSERETURN
1745 PRINT"{clear}":dr$="mcr.":GOSUB750:GETKEYa$:GOTO1500
1750 PRINT"{clear}Filename:";:GOSUB3490:f$=en$:OPEN1,dv%(dv),2,"0:mcr."+f$+",p,r":IFst<>0THENDCLOSE:GOTO1500
1755 PRINT"{clear}Appending{.*3}"f$:DO:INPUT#1,c1$(t1):PRINTc1$(t1):t1=t1+1:LOOPUNTILc1$(t1-1)="end":DCLOSE:PRINTds$:t1=t1-1:GETKEYa$:GOTO1500
1800 tx$="Checking Auto Macro":GOSUB1335:OPEN1,dv%(dv),2,"0:mcr.Auto"+f$:IFds<>0THENDCLOSE:RETURN
1801 tx$="Loading Auto Macro":GOSUB1335
1805 t1=-1:DO:t1=t1+1:INPUT#1,c1$(t1):s=st:LOOPUNTILs<>0:DCLOSE
1806 tx$="Executing Auto Macro":GOSUB1335
1810 GOTO1605
1900 s=VAL(br$):POKE2576,PEEK(2576)OR6:IFs=1200THENPOKE2576,PEEK(2576)OR8
1905 rc=cs/s:GOSUB1925:POKEsl,lo:POKEsh,hi:POKEDEC("202a"),lo:POKEDEC("202b"),hi
1910 IFs=1200THENrc=cs/s*.966:GOSUB1925
1915 POKE2582,lo:POKE2583,hi
1920 rc=cs/s/2-100:GOSUB1925:POKE2578,lo:POKE2579,hi:RETURN
1925 hi=INT(rc/256):lo=INT(rc-hi*256):RETURN
1930 WINDOW1,2,78,20,1:qq$=br$:PRINT"{clear}Baud Rate:"br$:PRINT"Baud Rate >";:GOSUB3490:br$=en$
1935 IF VAL(br$)<110ORVAL(br$)>2400THENbr$=qq$:PRINT"{clear}":RETURN
1940 GOSUB1900:RETURN
1942 PRINT"{clear}{down}Block Size:";:GOSUB2170:IFi$=""THENPRINT"{clear}":RETURN
1943 bs=VAL(i$):IFbs<40THENbs=40
1944 IFbs>255THENbs=255
1945 PRINT"{clear}":IFcn=1THENGOTO3600:ELSERETURN
1950 PRINT"{clear}"CHR$(14);:tx$=LEFT$("Last #:"+nm$,25):GOSUB1335:WINDOW1,1,79,20:POKE3069,cm
1955 a$="":b$="":FORt=1TO8:FORtt=1TOLEN(fu$(t)):a$=MID$(fu$(t),tt,1):IFa$="{arrow left}"THENa$=CHR$(13)
1960 b$=b$+a$:NEXT:KEYt,b$:b$="":NEXT
1965 PRINTTAB(24)"{ct g}[HOME] for Menu (Alt-? for help)":PRINT:WINDOW1,3,78,20,1
1970 GOSUB1165:GOTO4600
1999 PRINT"{cm t*78}";:RETURN
2000 REM start
2002 PRINTcr$"Filename:";:GOSUB3490:IFen$=""ORfl=1THENPRINT"Aborted.":RETURN:ELSEnf$=en$
2006 PRINTps$"{down}File Type (P, S, or U) -> ";:GETKEYa$:IFa$=CHR$(27)THENRETURN
2008 IFa$="p"THENsa=0:t=1:GOTO2016
2010 IFa$="s"THENsa=2:t=2:GOTO2016
2012 IFa$="w"THENsa=0:t=3:GOTO2016
2014 GOTO2006
2016 CLOSE87:OPEN87,dv%(dv),0,"$0:"+nf$:IFds>1THENPRINT"{ct g}"ds$:CLOSE87:GOTO2002
2018 FORzm=1TO35:GET#87,km$:NEXT:GET#87,kk$:CLOSE87:km=ASC(km$+CHR$(0))+256*ASC(kk$+CHR$(0))
2020 PRINTps$t$(t)"{down}":OPEN2,dv%(dv),sa,nf$:IFxm=1THENuc=0:GOTO2218
2022 SYSDEC("20b7"):POKEDEC("201e"),t:SYSDEC("20ae"):IFPEEK(512)=1THENCLOSE2:GETa$:RETURN
2024 PRINT" ";:GOSUB2026:SYSDEC("20b7"):POKEDEC("201b"),bs:SYSDEC("20a8"):CLOSE2:PRINTcr$cr$:RETURN
2026 IFkz=7THEN2030:ELSEPRINT"File contains:"+STR$(km)+" blk(s)."
2028 km=km*255/VAL(br$)/6+km*3/bs+km*.015:km=INT(km*10)/10:PRINT"Transfer Time:"km" min(s). at "br$" bps. with "bs" chars/blk.":PRINT
2030 km=0:km$="":kk$="":PRINTCHR$(20)tt$;"{space*9}10{space*8}20{space*8}30{space*8}40{space*8}50{space*8}60{space*8}70{space*5}78"
2032 PRINT"{-*78}":RETURN
2034 REM
2038 GOSUB2052:IF t=-1THEN2000
2040 GOTO2148
2042 kz=7:IFxm=1THEN2252
2044 PRINTcr$"Filename:";:GOSUB3490:IFen$=""ORfl=1THENPRINT"Aborted.":RETURN:ELSEnf$=en$
2046 SYSDEC("20b7"):SYSDEC("20ab"):IFPEEK(512)=1THENGETa$:RETURN
2048 t$=","+MID$(ty$,PEEK(DEC("201e")),1)+",w":PRINTps$"{down}File Type : "t$(PEEK(DEC("201e")))"{down}":OPEN2,dv%(dv),1,"0:"+nf$+t$:SLEEP1:SYS59074:PRINT" ";:GOSUB2026
2050 SYSDEC("20b7"):SYSDEC("20a5"):CLOSE2:SLEEP1:POKE2585,PEEK(2584):PRINTcr$cr$:RETURN
2052 REM
2054 PRINT"{down*2}Reading Directory{.*4}":a$="{!*18}":OPEN2,dv%(dv),0,"$:*"
2056 FORt=1TO25:GET#2,b$:NEXT:t=-1
2058 BANK15:SYSDEC("209f"):IFst=0THENt=t+1:a$(t)=a$:GOTO2058
2060 CLOSE2:p=t:IFt=-1THENPRINT"{clear}{down*5}No Files on disk{!*3}":PRINT"{down}RETURN to Continue":GETKEYa$:RETURN
2062 IF i=3 THEN 2068
2064 PRINT"{home}{clear}{space*11}ESC: Exit{space*7}HOME: Return to Top{space*10}R: Reverse Field"
2066 PRINT"{space*3}CURSOR Keys: to Move{space*3}SPACE: Select/Deselect{space*3}RETURN: to Continue":GOSUB1999:WINDOW0,3,78,20,1
2068 FORt=p+1TO80:a$(t)="":NEXT:RETURN
2070 PRINT"{clear}";
2072 PRINT"{home}";:FORt=0TO19:PRINTa$(t)"{reverse off}"TAB(20)a$(t+20)"{reverse off}"TAB(40)a$(t+40)"{reverse off}"TAB(60)a$(t+60):NEXT
2074 PRINTCHR$(27)"q";
2076 t=0:PRINT"{home}{ct o}"a$(t)CHR$(143)"{left*18}";
2078 GETKEYa$:IFa$<>"{up}"THEN2084
2080 PRINTa$(t):t=t-1:IFt<0THENt=0:GOTO2076
2082 GOTO2136
2084 IFa$<>"{down}"THEN2090
2086 PRINTa$(t):t=t+1:IFt>pTHENt=p
2088 GOTO2136
2090 IFa$<>"{right}"THEN2096
2092 PRINTa$(t):t=t+20:IFt>pTHENt=t-20
2094 GOTO2136
2096 IFa$<>"{left}"THEN2102
2098 PRINTa$(t):t=t-20:IFt<0THENt=t+20
2100 GOTO2136
2102 IFa$<>" "THEN2108
2104 IFLEFT$(a$(t),1)<>"{reverse on}"THENa$(t)="{reverse on}"+a$(t):GOTO2136
2106 a$(t)=RIGHT$(a$(t),18):PRINT"{reverse off}";:GOTO2136
2108 IFa$<>CHR$(13)THEN2122
2110 PRINT"{home}{reverse off}";:FORt=0TO19:IFASC(a$(t))=18THENPRINTa$(t)"{reverse off}";:ELSEPRINT"{space*18}{reverse off}";
2112 IFASC(a$(t+20))=18THENPRINTTAB(20)a$(t+20)"{reverse off}";:ELSEPRINT"{space*18}{reverse off}";
2114 IFASC(a$(t+40))=18THENPRINTTAB(40)a$(t+40)"{reverse off}";:ELSEPRINT"{space*18}{reverse off}";
2116 IFASC(a$(t+60))=18THENPRINTTAB(60)a$(t+60);:ELSEPRINT"{space*18}"
2118 NEXT:PRINT"{space*28}{ct o}Please Verify (Y/N)"CHR$(143):GETKEYa$:IFa$<>"y"THEN2072
2120 RETURN
2122 IFa$<>"r"THEN2128
2124 FORt=0TOp:IFASC(a$(t))=18THENa$(t)=RIGHT$(a$(t),18):ELSEa$(t)="{reverse on}"+a$(t)
2126 NEXT:PRINT"{reverse off}";:GOTO2072
2128 IFa$<>CHR$(27)THEN2132
2130 GOTO2000
2132 IFa$<>"{home}"THEN2078
2134 PRINTa$(t):t=0
2136 PRINT"{home}";:IFt>19THENFORr=1TOINT(t/20):PRINT"{right*20}";:NEXT
2138 IFt-INT(t/20)*20>0THENFORr=1TOt-INT(t/20)*20:PRINT"{down}";:NEXT
2140 PRINT"{ct o}"a$(t)"{left*18}"CHR$(143);:GOTO2078
2142 GOSUB2052:IFt=-1THENRETURN
2144 GOSUB2070:PRINT"{clear}{down*5}Setting Up{.*3}{down}":x=0:FORt=0TOp:IFASC(a$(t))=18THENa$(x)=RIGHT$(a$(t),18):x=x+1
2146 NEXT:p=x-1
2148 PRINT"{home}{clear}"TAB(35)"Multi-Send{down}":t1=0
2150 PRINT"{home}{down*2}";:GOSUB2182:IFt1>1THENPRINTTAB(31)"{cm -}"a$(t1-2)TAB(50)"{cm -}":ELSEGOSUB2184
2152 IFt1>0THENPRINTTAB(31)"{cm -}"a$(t1-1)TAB(50)"{cm -}":ELSEGOSUB2184
2154 PRINTTAB(24)"Now -->{cm -}{reverse on}"a$(t1)TAB(50)"{reverse off}{cm -}<-- Sending":IFt1<pTHENPRINTTAB(31)"{cm -}"a$(t1+1)TAB(50)"{cm -}":ELSEGOSUB2184
2156 CLOSE87:OPEN87,dv%(dv),0,"$0:"+a$(t1):IFds>1THENPRINT"{ct g}"ds$:CLOSE87:GOTO2002
2158 FORzm=1TO35:GET#87,km$:NEXT:GET#87,kk$:CLOSE87:km=ASC(km$+CHR$(0))+256*ASC(kk$+CHR$(0))
2160 IFt1<p-1THENPRINTTAB(31)"{cm -}"a$(t1+2)TAB(50)"{cm -}":ELSEGOSUB2184
2162 GOSUB2182
2164 PRINTTAB(33)p-t1"{left} Files To Go {down*2}"CHR$(27)"@":nf$=a$(t1)
2166 IFRIGHT$(nf$,1)=" "THENnf$=LEFT$(nf$,LEN(nf$)-1):GOTO2166
2168 PRINT#5,"{ct i*20}";:IFRIGHT$(nf$,1)="s"THENsa=2:t=2:ELSEsa=0
2170 IFRIGHT$(nf$,1)="p"THENt=1
2172 PRINT#5,nf$:SLEEP1:SYSDEC("20b7"):POKEDEC("201e"),t:SYSDEC("20ae"):IFPEEK(512)=1THENCLOSE2:GETa$:RETURN
2174 OPEN2,dv%(dv),sa,nf$:IFds<>0THENPRINT"{down}"ds$:CLOSE2:GOTO2186
2176 PRINT" ";:GOSUB2026:SYSDEC("20b7"):POKEDEC("201b"),bs:SYSDEC("20a8"):CLOSE2:IFPEEK(512)=1THENGETa$:RETURN
2178 SLEEP2:t1=t1+1:IFt1>pTHEN2186
2180 GOTO2150
2182 PRINTTAB(31)"{yellow}{cm g}{cm t*18}{cm m}":RETURN
2184 PRINTTAB(31)"{cm -}{space*18}{cm -}":RETURN
2186 PRINT#5,"{ct i*20}"CHR$(4):RETURN
2188 PRINT"{clear}{down*5}{space*20}Please Wait while sender is prepairing {.*3} ":kz=7
2190 GET#5,a$:GETb$:IFb$=""ANDa$=""THEN2190
2192 IFb$=CHR$(27)THENRETURN
2194 IFa$="{ct i}"THENPRINT"{clear}{down}"TAB(34)"Multi-Receive":nf$="":GOTO2198
2196 GOTO2190
2198 GET#5,a$:IFa$=""THEN2198
2200 IFa$="{ct i}"ORa$=CHR$(34)THEN2198
2202 IFa$=CHR$(4)THENRETURN
2204 GETb$:IFb$=CHR$(27)THENRETURN
2206 IFa$=CHR$(13)THEN2210
2208 nf$=nf$+a$:GOTO2198
2210 OPEN2,dv%(dv),1,nf$+",w":IFds<>0THENPRINT"{down}{reverse on}"ds$:RETURN
2212 PRINT"{down*2}Receiving : "nf$"{down*2}":SYSDEC("20b7"):SYSDEC("20ab"):IFPEEK(512)=1THENGETa$:RETURN
2214 PRINT" ";:GOSUB2026:SYSDEC("20b7"):SYSDEC("20a5"):CLOSE2:GOTO2188
2216 GOTO3490
2218 GET#5,a$:a=ASC(a$+CHR$(0)):IFa=21THEN2224
2220 uc=uc+1:IFuc<500THEN2218
2222 PRINTtb$"{down*2}No NAK recieved - Upload ABORTED":GOTO2250
2224 PRINT" ";:GOSUB2026:SYSxc:uc=0:IFPEEK(xc+8)=0THENmz=0:GOTO2232
2226 IFPEEK(xc+8)=3THENPRINT:PRINTt"Upload Cancelled."
2228 IFPEEK(xc+8)=4THENPRINT:PRINTt"More than 10 tries for a single block"
2230 PRINT#5,er$:GOTO2250
2232 uc=0
2234 PRINT#5,CHR$(4);:PRINT:PRINT"First <EOT> SENT ";
2236 FORa=1TO2000:GET#5,a$:IFa$=""THENNEXTa:uc=uc+1:IFuc<11THEN2234
2238 IFASC(a$+CHR$(0))=6THENPRINT" First <EOT> ACKNOWLEDGED":GOTO2250
2240 PRINT" <EOT> NAK'ed"
2242 uc=0
2244 PRINT#5,CHR$(4);:PRINT:PRINT"Second <EOT> SENT ";
2246 FORa=1TO2000:GET#5,a$:IFa$=""THENNEXTa:uc=uc+1:IFuc<11THEN2234
2248 IFASC(a$+CHR$(0))=6THENPRINT" Second <EOT> ACKNOWLEDGED":ELSEPRINTt" <EOT> NOT ACKNOWLEDGED"
2250 SLEEP3:CLOSE2:PRINTcr$cr$:RETURN
2252 PRINTcr$"Filename:";:GOSUB2216:IFen$=""ORfl=1THENRETURN
2254 PRINTcr$"File Type (Prg or Seq)? ";:GETKEYa$:IFa$=CHR$(13)THENPRINT"Aborted.":RETURN
2256 IFa$="p"THENt=1:GOTO2262
2258 IFa$="s"THENt=2:GOTO2262
2260 GOTO2254
2262 mg=xc+8:PRINTt$(t):PRINT:SCRATCH(en$):OPEN2,dv%(dv),2,"0:"+en$+","+a$+",w":IF ds>1THENPRINTds$:DCLOSE:GOTO2274
2264 POKEmg,2:PRINT" ";:GOSUB2026:SYSxc+4:a=PEEK(mg):PRINT:IFa=2THENa$="Aborted":PRINT#5,er$
2266 IFa=0THENa$="End Of Transmission Received"
2268 IFa=1THENa$="Synchronization Lost":PRINT#5,er$
2270 IFa=3THENa$="Download Cancelled, CTRL X Received"
2272 IFa=4THENa$="More Than 10 Tries For Single Block":PRINT#5,er$
2274 PRINTa$:PRINTcr$cr$:GOTO2250
2615 dp=-dp+1:POKE3068,dp:RETURN
2640 cm=-cm+1:POKE3069,cm:RETURN
3000 mn$="Phone Book"
3005 co$(1)="Call No.":co$(2)="Autodialer":co$(3)="Change No.":co$(4)="Erase No.":co$(5)="Add No."
3010 co$(6)="Multi-Dial":co$(7)="Load PhoneBook":co$(8)="Save PhoneBook":co$(9)="PB Name:"+pn$:co$(10)="Restore Backup":co$(11)="Main Menu"
3015 co=11:GOSUB26000
3020 ONaGOTO3035,3065,3050,3080,3091,3145,2420,2435,2445,3560,3030
3025 GOTO3000
3030 RETURN
3035 GOSUB3110:GETKEYa$:a=ASC(a$)-64:IFa<1ORa>hh+1THEN3000
3040 IFa=hh+1THENGOTO3000
3045 nm$=an$(a):br$=a1$(a):GOSUB1900:cm=a2(a):GOTO510
3050 GOSUB3110:GETKEYa$:a=ASC(a$)-64:IFa<1ORa>hh+1THEN3050
3055 IFa=hh+1THENGOTO3000
3056 PRINT"{clear}Name: "aa$(a):PRINT"Number: "an$(a):PRINT"Baud: "a1$(a):PRINT"Mode: "cm$(a2(a))
3057 PRINT"{down}Name:";:en$="Empty":GOSUB3490:aa$(a)=en$: PRINT"Number:";:en$="{0*7}":GOSUB3490:an$(a)=en$
3061 PRINT"Baud (1) 300 (2) 1200:";:GETKEYa$:IFa$="2"THENPRINT"1200":a1$(a)="1200":ELSEPRINT"300":a1$(a)="300"
3062 PRINT"Mode (1) Ascii (2) Comscii:";:GETKEYa$:IFa$="1"THENPRINT"Ascii":a2(a)=0:ELSEPRINT"Comscii":a2(a)=1
3063 GOTO3000
3065 GOSUB3110:GETKEYa$:a=ASC(a$)-64:IFa<1ORa>26THEN1725
3070 IFa=hh+1THENGOTO3000
3075 nm$=an$(a):br$=a1$(a):GOSUB1900:cm=a2(a):f1=1:GOSUB510:SLEEP4:f1=0:GOSUB435:RETURN
3080 GOSUB3110:GETKEYa$:a=ASC(a$)-64:IFa<0ORa>hh+1THEN3000
3085 IFa=hh+1THENGOTO3000
3090 FORt=aTOhh:a1$(t)=a1$(t+1):a2(t)=a2(t+1):aa$(t)=aa$(t+1):an$(t)=an$(t+1):NEXT:hh=hh-1:GOTO3000
3091 IFhh=25THENWINDOW1,2,78,20,1:PRINT"{clear}Phonebook Full{.*3}Press a Key.";:GETKEYa$:GOTO3000
3092 hh=hh+1:PRINT"{clear}Name:";:GOSUB3490:aa$(hh)=en$:IFen$=""THENaa$(hh)="Empty"
3093 PRINT"Number:";:GOSUB3490:an$(hh)=en$:IFen$=""THENan$(hh)="{1*7}"
3094 PRINT"Baud (1) 300 (2) 1200:";:GETKEYa$:IFa$="2"THENPRINT"1200":a1$(hh)="1200":ELSEPRINT"300":a1$(hh)="300"
3095 PRINT"Mode (1) Ascii (2) Comscii:";:GETKEYa$:IFa$="1"THENPRINT"Ascii":a2(hh)=0:ELSEPRINT"Comscii":a2(hh)=1
3099 GOTO3000
3100 PRINT"{clear}Are You Sure?";:GETKEYa$:IFa$<>"y"THENGOTO3000
3105 SCRATCH"pc.phone":DOPEN#1,"pc.phone,s,w":FORt=1TOhh:PRINT#1,CHR$(34)+aa$(t):PRINT#1,an$(t):PRINT#1,a1$(t):PRINT#1,a2$(t):NEXT:DCLOSE:GOTO3000
3110 IFml=0THENWINDOW1,2,78,20,1
3115 IFaa$(1)=""THENhh=0
3120 IFml=0THENpm$="Phonebook Menu":ELSEpm$="Start Multi-Dial"
3125 IFhh>13THENBEGIN:FORt=1TO13:PRINTCHR$(t+96)". "aa$(t)TAB(20)":"an$(t):NEXT:PRINT"{home}";:FORt=14TOhh:PRINTTAB(40)CHR$(t+96)". "aa$(t)TAB(60)":"an$(t):NEXT
3130 PRINTTAB(40)CHR$(t+96)". "pm$"{home}";:RETURN:BEND
3135 IFhh<>0THENFORt=1TOhh:PRINTCHR$(t+96)". "aa$(t)TAB(20)":"an$(t):NEXT:ELSEt=1
3140 PRINTCHR$(t+96)". "pm$"{home}";:RETURN
3145 md=1:ml=1:FORt=1TO20:md$(t)="":NEXT
3150 WINDOW1,16,78,20,1
3155 IFmd>1THENPRINT"{home}";:FORt=1TO4:PRINTt"{left}."md$(t):NEXT
3160 IFmd>5THENPRINT"{home}";:FORt=5TO8:PRINTTAB(20)t"{left}."md$(t):NEXT
3165 IFmd>9THENPRINT"{home}";:FORt=9TO12:PRINTTAB(40)t"{left}."md$(t):NEXT
3170 IFmd>13THENPRINT"{home}";:FORt=13TO16:PRINTTAB(60)t"{left}."md$(t):NEXT
3175 WINDOW1,2,78,15,1:GOSUB3110
3180 GETKEYa$:IFa$=CHR$(27)THENPRINT"{clear}":GOTO3000
3185 a=ASC(a$)-64:IFa<1ORa>26THEN3175
3190 IFa=hh+1THENml=0:GOTO3205
3195 md$(md)=aa$(a):ml$(md)=an$(a):md=md+1:IFmd=17THENPRINT"{clear}":GOTO3205
3200 GOTO3150
3205 IFmd=1THENGOTO3000
3210 FORmc=1TOmd-1:f1=1:nm$=ml$(mc):br$=a1$(mc):GOSUB1900:cm=a2(mc):PRINT"{clear}Dialing{.*3}"md$(mc):GOSUB515:f1=0
3215 tt=0:DO:GETa$:tt=tt+1:LOOPUNTILtt=INT(tt%*2.66666667)OR(PEEK(56577)AND16)=0ORa$<>""
3220 IFtt=INT(tt%*2.66666667)THENGOSUB435:SLEEP1:NEXT:GOTO3205
3225 IFa$<>""THENGOSUB435:mc=md:NEXT:GOTO3000
3230 mc=md:NEXT:GOSUB1295:GOTO300
3235 FORii=1TOLEN(nm$)
3240 pp=ASC(MID$(nm$,ii,1))
3245 IF (pp=35)OR(pp=42)OR(pp=43)THEN 3255
3250 IF(pp<48)OR(pp>57)THEN pp=0:GOTO3265
3255 pp=pp-48
3260 IF pp=0THEN pp=10
3265 pd(ii)=pp
3270 NEXT
3275 REM pulse dialing
3280 GOSUB595
3285 WINDOW1,2,78,20,1:CHAR1,34,0,"Dialing{.*3}":WINDOW1,2,78,20
3290 SLEEP1
3295 GOSUB595:SLEEP1:FORdd=1TOLEN(nm$)
3300 IF pd(dd)>0ANDpu=0THEN3360:ELSEIFpd(dd)>0ANDpu<>0THEN3315
3305 IF pd(dd)=-5 THEN FORii=1TO800:NEXT ii
3310 NEXTdd:BANK0:IFf1=0THENGOTO180:ELSERETURN
3315 FORii=1TOpd(dd)
3320 GOSUB435:IFpu=3THENFORjj=1TO40:NEXT
3325 IFpu=2THENFORjj=1TO20:NEXT
3330 GOSUB595:IFpu=3THENFORjj=1TO25:NEXT
3335 IFpu=2THENFORjj=1TO10:NEXT
3340 NEXTii
3345 FORii=1TO200:NEXT
3350 GETd$:IFd$<>""THENGOSUB435:RETURN
3355 NEXTdd:BANK15:IFf1=0THENGOTO180:ELSERETURN
3360 IFmoTHEN3375
3365 FORii=1TOpd(dd):POKE56577,PEEK(56577)OR32:POKE56579,PEEK(56579)OR32:POKE56577,PEEK(56577)AND223:NEXT
3370 FORt=1TO300:NEXT:NEXTdd:BANK15:IFf1=0THENGOTO180:ELSERETURN
3375 FORii=1TOpd(dd):POKE56579,PEEK(56577)OR32:POKE56577,PEEK(56577)AND223:POKE56577,PEEK(56577)OR32:NEXT
3380 FORt=1TO400:NEXT:NEXTdd:BANK15:IFf1=0THENGOTO180:ELSERETURN
3385 PRINT"Filename:";:en$="*":GOSUB3490:f$=en$
3386 IFf$=""THENRETURN
3390 DOPEN#6,(f$):IFds<>0THENCLOSE6:RETURN
3395 DO:GETb1$:GET#5,b2$:GET#6,a$:s=st:PRINT#5,CHR$(PEEK(5376+ASC(a$)));:PRINTa$;:LOOPUNTILs<>0ORb1$=CHR$(27)ORb2$=CHR$(27):CLOSE6
3400 RETURN
3405 WINDOW1,1,78,2,1:CHAR1,33,0,"FunctionKeys":WINDOW1,2,78,20,1
3410 FORt=1TO8:PRINTt"{left}. "+CHR$(34)+CHR$(20)+fu$(t):NEXT
3415 PRINT9"{left}. Configure Menu"
3420 GETKEYa$:a=VAL(a$):IFa<1ORa>9THEN3420
3425 IFa=9THENGOTO3600
3430 PRINT"{clear}Function:"a">"CHR$(34)CHR$(20);:GOSUB3490:IFen$<>""THENfu$(a)=en$:ELSEfu$(a)="{arrow left}"
3435 GOTO3405
3440 PRINT"{clear}Are you sure?":GETKEYa$:IFa$="n"THEN3405
3445 RETURN
3450 IFpu=0THENpu=1:GOTO3600
3455 IFpu=1THENpu=2:GOTO3600
3460 IFpu=2THENpu=3:GOTO3600
3465 IFpu=3THENpu=0:GOTO3600
3475 IFrg=1THENrg=0:CHAR1,18,17,"{space*4}"
3480 RETURN
3485 PRINT"{ct b}{.*4}+{.*4}1{.*4}+{.*4}2{.*4}+{.*4}3{.*4}+{.*4}4{.*4}+{.*4}5{.*4}+{.*4}6{.*4}+{.*4}7{.*4}+{.*3}":RETURN
3490 en$=""
3495 GETKEYa$:IFed%=1ANDa$=CHR$(27)ANDLEN(en$)=0THENBEGIN:GOSUB6600:IFqu%=0THENGOTO3490:ELSERETURN:BEND
3505 IFa$=cr$ORb$=cr$ORLEN(en$)=ln%THENPRINT:RETURN
3510 IFa$=CHR$(20)ORb$=CHR$(20)THENBEGIN
3515 IFLEN(en$)=0THENa$="":b$="":ELSEen$=LEFT$(en$,LEN(en$)-1):PRINTa$;
3520 BEND
3525 GOSUB7000:IFa$=""THEN3495
3530 IFCHR$(20)=a$THENa$=""
3540 PRINTa$;:en$=en$+a$:GOTO3495
3550 RENAME"pc.config"TO"temp":RENAME"pc.config/bak"TO"pc.config":RENAME"temp"TO"pc.config/bak":RETURN
3560 RENAME(pn$)TO"temp":RENAME(pn$+"/bak")TO(pn$):RENAME"temp"TO(pn$+"/bak"):RETURN
3600 mn$="Configure Menu"
3605 co$(1)="Dial Type:"+ph$(ph):co$(2)="Modem:"+mo$(mo):co$(3)="Pulse:"+pu$(pu):co$(4)="Function Keys":co$(5)="Block Size:"+STR$(bs)
3610 co$(6)="Save Config File":co$(7)="Load Config File":co$(8)="CharSet:"+ch$
3615 co$(9)="Colors":co$(10)="Modem Params":co$(11)="Restore Backup":co$(12)="Main Menu"
3620 cn=1:co=12:GOSUB26000
3625 ONaGOTO1710,3705,3450,3405,1942,3650,3670,3640,1300,5000,3550,3635
3630 GOTO3600
3635 RETURN
3640 PRINT"{clear}Charset #:";:GOSUB3490:IFen$=""THEN3600
3645 ch$=en$:ch=VAL(ch$):GOSUB4095:GOTO3600
3650 PRINT"{clear}Are You Sure?";:GETKEYa$:IFa$="n"THENGOTO3600
3651 PRINT"{clear}";:SCRATCH"pc.config/bak":RENAME"pc.config"TO"pc.config/bak"
3655 tx$="Saving Configure File{.*3}":GOSUB1335:SCRATCH"pc.config":SCRATCH"pc.char":DOPEN#1,"pc.char,s,w":PRINT#1,ch$:DCLOSE#1
3660 DOPEN#1,"pc.config,s,w":PRINT#1,mo:PRINT#1,pu:PRINT#1,ph:PRINT#1,bs:PRINT#1,tn$:PRINT#1,tf$:PRINT#1,tr:PRINT#1,sk%:PRINT#1,rp%:PRINT#1,aa%:PRINT#1,tt%
3662 PRINT#1,pn$:PRINT#1,ee%:PRINT#1,pa%:PRINT#1,wl%:PRINT#1,sb%:PRINT#1,hs%:DCLOSE#1:SCRATCH"pc.keys":DOPEN#1,"pc.keys,s,w"
3665 FORt=1TO8:PRINT#1,CHR$(34)+fu$(t):NEXT:PRINT#1,c1:PRINT#1,c2:CLOSE1:GOSUB1325:GOTO3600
3670 PRINT"{clear}Are You Sure?";:GETKEYa$:IFa$="n"THENGOTO3600
3675 c2=1:tx$="Loading Config File{.*3}":GOSUB1335
3680 OPEN1,dv%(dv),2,"pc.config,s,r":IFst<>0THENtx$="Config File Not Present{.*3}":CLOSE1:GOSUB1335:DCLEAR:SLEEP1:GOTO3690
3685 INPUT#1,mo:INPUT#1,pu:INPUT#1,ph:INPUT#1,bs:INPUT#1,tn$:INPUT#1,tf$:INPUT#1,tr:INPUT#1,sk%:INPUT#1,rp%:INPUT#1,aa%:INPUT#1,tt%:INPUT#1,pn$
3686 INPUT#1,ee%:INPUT#1,pa%:INPUT#1,wl%:INPUT#1,sb%:INPUT#1,hs%:DCLOSE#1:GOSUB5800:tx$="Loading Function Keys{.*3}":GOSUB1335
3690 OPEN1,dv%(dv),2,"pc.keys,s,r":IFds<>0THEN3700
3695 FORt=1TO8:INPUT#1,fu$(t):NEXT:INPUT#1,c1:INPUT#1,c2:COLOR6,c1:COLOR5,c2
3700 CLOSE1:IFcn=1THENGOTO3600:ELSERETURN
3705 IFmo=0THENmo=1:ph=1:GOTO3600
3710 IFmo=1THENmo=2:ph=0:GOTO3600
3715 IFmo=2THENmo=3:ph=1:GOTO3600
3720 IFmo=3THENmo=0:ph=0:POKE56577,2:GOTO3600
3740 COLOR5,c2:GETa$:PRINTCHR$(27)"n";
3741 IFa$="@"THENPRINTcr$"Disk Command>";:GOSUB3490:OPEN15,dv%(dv),15,en$:PRINT"Status:"ds$:DCLOSE:DCLEAR
3742 IFa$="1"THENGOSUB2615:GOSUB1165:a$=""
3745 IFa$="u"THENtx$="[ Protocol: "+xm$(xm)+" ]":GOSUB1330:GOSUB2002
3746 IFa$="^"THENBEGIN:tx$="[ Sending Codes ]":GOSUB1330:SLEEP1:FORtt=0TO255:PRINT#5,CHR$(tt);:FORt=1TO250:NEXT:GETb$:IFb$<>""THENtg=0:tt=255
3747 tx$="[ Code:"+STR$(tt)+"]":GOSUB1330:GET#5,a$:IFa$<>""THENtg=tt:tt=255
3748 NEXT:IFa$<>""THENtx$="[ Response at Code"+STR$(tg)+"]":GOSUB1330:ELSEtx$="[ No Response ]":GOSUB1330:BEND
3750 IFa$="d"THENtx$="[ Protocol: "+xm$(xm)+" ]":GOSUB1330:GOSUB2042
3751 IFa$="{home}"THENPRINT"{clear}";
3755 IFa$="o"THENb3=1:POKE3071,1:GOSUB1165:RETURN
3760 IFa$="c"THENb3=0:POKE3071,0:GOSUB1165:RETURN
3765 IFa$="q"THENtx$="[ Buffer Cleared ]":GOSUB1330:POKE250,0:POKE251,128:BANK1:POKE32768,0:POKE3071,0:POKE3070,0:BANK15
3770 IFa$="b"THENtx$="[ Buffer:"+STR$(32512-(32512-((256-PEEK(251))*256)+PEEK(250)))+"]":GOSUB1330
3771 BANK1:IF(a$="p"ORa$="s")ANDPEEK(32768)=0THENa$="":tx$="[ Buffer Empty ]":GOSUB1330
3772 BANK15
3775 IFa$="p"THENBEGIN:PRINTcr$"[ Print Buffer ]":PRINT"[S]creen, [P]rinter or [M]odem:":GETKEYa$:IFa$<>"p"ANDa$<>"s"ANDa$<>"m"THEN3780
3776 tx$="[ Sending Buffer ]":GOSUB1330
3780 IFa$="p"THENOPEN1,pr%,7:a$="":SYS3707:PRINT#1:CLOSE1
3785 IFa$="s"THENOPEN1,3:a$="":SYS3707:CLOSE1
3790 IFa$="m"THENa$="":GOSUB3933
3791 tx$="[ End of Buffer ]":GOSUB1330:BEND
3795 IFa$="s"THENBEGIN:PRINTcr$"Name Of Buffer [S]:";:GOSUB3490:IFen$<>""THENf$=en$:tx$="[ Saving "+f$+" ]":GOSUB1330:OPEN1,dv%(dv),2,"0:"+f$+",w":ELSE3925
3800 IFds<>0THENPRINTds$:CLOSE1:ELSEGOSUB3930:CLOSE1:BEND
3805 IFa$="l"THENBEGIN:PRINTcr$"Name Of Buffer [L]:";:GOSUB3490:IFen$<>""THENf$=en$:OPEN1,dv%(dv),2,"0:"+f$:ELSE3925
3810 IFds<>0THENPRINTds$:CLOSE1:ELSEGOSUB3935:BEND
3820 IFa$="+"THENGOSUB595:tx$="[ Pick-Up ]":GOSUB1330
3825 IFa$="-"THENGOSUB435:tx$="[ Hang-Up ]":GOSUB1330
3830 IFa$="a"THENBEGIN:PRINTcr$"Number:";:GOSUB3490:IFVAL(en$)<>0THEN:nm$=en$:f1=1:cl%=3:GOSUB510:cl%=2:RETURN
3835 IFen$=""THEN3850
3840 nm$="":FORt=1TO25:IFLEFT$(aa$(t),LEN(en$))=en$THENf1=1:nm$=an$(t):cl%=3:GOSUB510:cl%=2:t=25:PRINTcr$
3845 NEXT:IFnm$=""THENtx$="Name Not Found.":GOSUB1330:a$="n":GOTO3870:BEND
3850 IFa$="?"THENGOSUB1170:GOTO3741
3851 IFa$="*"THENf1=1:cl%=3:GOSUB510:cl%=2
3855 IFa$="t"THENBEGIN:ba=VAL(br$)
3860 PRINTcr$"Blocks:";:GOSUB3490:bl=VAL(en$):IFen$=""THENGOTO3925
3865 tm=((bl*256)/(ba/8))/60*(1+1/3):PRINT"Time:"tm:BEND
3870 IFa$="n"THENPRINT"{clear}";:FORt=1TOhhSTEP4:PRINTaa$(t)TAB(20)aa$(t+1)TAB(40)aa$(t+2)TAB(60)aa$(t+3):NEXT:a$="a":GOTO3830
3875 IFa$="5"THENGOSUB3385
3880 IFa$="1"THENGOSUB2615
3885 IFa$="3"THENBEGIN:PRINTcr$"Enter Hex Number:";:GOSUB3490:IFen$=""THEN3910
3886 tx$="Decimal:"+STR$(DEC(en$)):GOSUB1330:BEND
3890 IFa$="2"THENGOSUB2640:GOSUB1165:a$=""
3895 IFa$="6"THENBEGIN:qq$=br$:PRINTcr$"["br$"] New Baud:";:GOSUB3490:IFVAL(en$)<110ORVAL(en$)>2400THENRETURN:ELSEbr$=en$:GOSUB1900:BEND
3900 IFa$="4"THENBEGIN:PRINTcr$"Enter Decimal Number:";:GOSUB3490:IFVAL(en$)=0THEN3910
3901 tx$="Hex:"+HEX$(VAL(en$)):GOSUB1330:BEND
3905 IFa$="{pound}"THENPRINTcr$"Text:";:GOSUB3490:OPEN4,pr%,7:PRINT#4,en$:PRINT#4:CLOSE4
3910 IFa$="8"THENDIRECTORYu(dv%(dv)):PRINTds$:DCLEAR:RETURN
3911 IFa$="m"THENGOSUB1705:tx$="[ Protocol: "+xm$(xm)+" ]":GOSUB1330:RETURN
3915 IFa$="7"THENGOSUB750
3916 IFASC(a$)>191ANDASC(a$)<219THENa=ASC(a$)-192:PRINT#5,CHR$(a);:a$="":POKE208,0:RETURN
3920 IFa$="h"THENPRINTcr$"Enter Hayes Command:";:GOSUB3490:PRINT#5,"at"+en$+cr$:SLEEP1
3921 IFa$="e"THENGOSUB6000:WINDOW1,1,78,1,1:PRINTTAB(24)"[HOME] for Menu (Alt-? for help)";:WINDOW1,3,78,20,1
3922 IFa$="r"THENPRINTcr$"[ Reading Peripheral Status ]":tm%=1:x=PEEK(236):y=PEEK(235):GOSUB4175:WINDOW1,3,78,20:CHAR1,x+1,y-3,"":PRINT"[ Done ]"
3929 IF(PEEK(211)AND8)<>8THENPOKE208,0:a$="":RETURN:ELSE3740
3935 tx$="[ Loading "+f$+" ]":GOSUB1330:BANK15:SYS3587:DCLOSE#1:DCLEAR
3945 tx$="[ End Of Load ]":GOSUB1330:BANK15:RETURN
4000 GOSUB1325:mn$="Disk Options"
4005 co$(1)="Directory":co$(2)="Disk Commands":co$(3)="Stats Of a File"
4010 co$(4)="Change Drive":co$(5)="Read File":co$(6)="Comscii/Ascii":co$(7)="File Copier"
4011 co$(8)="Main Menu":CLOSE15
4015 co=8:GOSUB26000
4020 ONaGOTO4035,4040,4155,2480,4500,10000,4800,4065
4025 GOTO4000
4035 PRINT"{clear}":WINDOW27,2,78,20,1
4036 PRINT"{clear}";:DIRECTORYu(dv%(dv)):PRINTds$:GETKEYa$:GOTO4000
4040 GOSUB900:GOTO4000
4065 RETURN
4090 tx$="Checking Character Set{.*3}":GOSUB1335
4091 OPEN1,dv%(dv),2,"pc.char":INPUT#1,ch$:DCLOSE#1:ch=VAL(ch$):BANK15:IFPEEK(3056)=chORch$=""THENRETURN
4095 BANK15:h=ch:OPEN1,dv%(dv),2,("pc.char"+STR$(ch)):IFst=0THENPOKE3056,ch:DCLOSE#1:ELSEch=h:PRINT"{clear}Character Set Not Present.{ct g}":DCLOSE#1:ch=h:GOSUB4135:RETURN
4100 tx$="Loading Character Set{.*3}":GOSUB1335:RENAME("pc.char"+STR$(ch))TO("pc.char"+STR$(ch)):IFds<>62THENBLOAD("pc.char"+STR$(ch)),b1,p55296,u(dv%(dv))
4105 RESTORE4115:FORi=0TODEC("2b"):READx$:POKEDEC("0200")+i,DEC(x$):NEXT
4110 BANK15:SYSDEC("0200")
4115 DATA a9,20,8d,a2,02,a9,22,8d,a3,02,a9,02,8d,a4,02,20,62,ff
4120 DATA a9,ad,8d,a2,02,a9,00,8d,a3,02,a9,ff,8d,a4,02,60
4125 DATA a2,01,bd,f0,f7,aa,ad,00,ff,60
4130 RETURN
4135 PRINT"{down*2}Do You Wish To See Choices?";:GETKEYa$:IFa$<>"y"THENRETURN
4140 PRINT"{clear}{down*2}Available Character Sets:":dr$="pc.char ":GOSUB750:GETKEYa$:RETURN
4145 ho=-ho+1:IFho=0THENGOSUB435:RETURN:ELSEGOSUB595:RETURN
4150 GOSUB435:SLEEP1:GOSUB595:SLEEP1:GOSUB435:SLEEP20:RETURN
4155 PRINT"{clear}Filename:";:en$="":GOSUB3490:f$=en$:IFf$=""THENGOTO4000
4160 OPEN1,dv%(dv),2,"0:"+f$:GET#1,a$:GET#1,b$:a=ASC(a$)+ASC(b$)*256:DO:GET#1,a$:x=x+1:LOOPUNTILst<>0:CLOSE1:PRINT:PRINT"Start:"a:PRINT"End:"a+x
4165 PRINT"Kilobytes:"x/1024:PRINT"Blocks:"x/256:PRINT"Bytes:"x:GETKEYa$:GOTO4000
4170 tx$="Checking Peripherals{.*3}":GOSUB1335
4175 dv%=0:TRAP4205:tt=0:FORt=8TO13
4180 p=1:GOSUB4215:IFp=1THENtt=tt+1:dv%(tt)=t:dv%=dv%+1:tx$="[ Drive #"+STR$(t)+" Active ]":GOSUB1335
4185 NEXT
4190 p=1:pr%=3:OPEN4,4,7:PRINT#4,"";:IFp=1THENCLOSE4:pr%=4:tx$="[ Printer Active ]":GOSUB1335
4191 GETa$:CLOSE4:DCLOSE
4196 IFtm%=1THENtm%=0:RETURN
4200 RETURN
4205 TRAP4205:IFer=5THENp=0:RESUMENEXT
4206 IFer=30THEN:RESUMENEXT
4210 GOTO4220
4215 OPEN1,t,15,"i0:":INPUT#1,a$:CLOSE1:RETURN
4220 PRINT"{clear}Error:"ERR$(er):END
4500 PRINT"{clear}File to Read:";:GOSUB3490:IFen$=""THENGOTO4000
4505 PRINT"{clear}":OPEN1,dv%(dv),2,en$:IFds<>0THENPRINTds$:DCLOSE:GETKEYa$:GOTO4000
4510 BANK15:SYS3584:BANK15:DCLOSE#1:DCLEAR
4515 PRINTcr$TAB(34)"Press Any Key!":GETKEYa$
4520 GOTO4000
4600 tr%=1:BANK15:SLOW:SYS2816:FAST:IFPEEK(3070)=255ANDPEEK(3071)=1THENtx$="[ Buffer Full and Closed ]":GOSUB1330:b3=0:POKE3071,0:GOSUB1165:GOTO4600
4605 IFPEEK(211)<>8THENtr%=0:RETURN
4610 GOSUB3740:GOTO4600
4700 tx$="Help":GOSUB1335
4701 WINDOW1,1,78,20,1:CHAR1,37,0,"Help":WINDOW1,2,78,20
4705 PRINT"{clear}{ct b}Subjects of Help:"
4710 OPEN1,8,2,"pc.Doc Index":IFst<>0THENPRINTds$:DCLOSE:DCLEAR:GETKEYa$:RETURN
4712 INPUT#1,a$:GOSUB4790:PRINTa$;:IFs<>0THEN4730
4713 INPUT#1,a$:GOSUB4790:PRINTTAB(40)a$;:IFs<>0THEN4730
4720 PRINT:GOTO4712
4730 PRINT:DCLOSE:DCLEAR:PRINT"Your Selection:";:GOSUB3490:IFen$=""THENRETURN
4735 sc$=en$:OPEN1,8,2,"pc.Doc Index":tx$=sc$:GOSUB1335:WINDOW1,2,78,20,1
4740 DO:INPUT#1,a$:INPUT#1,b:INPUT#1,c:s=st:LOOPUNTILs<>0ORsc$=a$
4745 IFs<>0ANDsc$<>a$THENDCLOSE:DCLEAR:PRINT"{clear}Selection not Present.";:GETKEYa$:GOTO4700
4750 s1%=b:s2%=c:DCLOSE:DCLEAR
4755 OPEN1,8,2,"pc.doc"
4760 PRINT"{clear}";:FORt=s1%TOs2%:RECORD#1,t:GOSUB4795
4761 GETa$:IFa$=CHR$(27)THENt=s2%
4764 NEXT
4765 DCLOSE:DCLEAR:GETKEYa$:GOTO4700
4790 INPUT#1,b$:INPUT#1,b$:s=st:RETURN
4795 b$="":DO:GET#1,a$:PRINTa$;:b$=b$+a$:LOOPUNTILa$=CHR$(13)ORLEN(b$)=80:RETURN
4800 mn$="FileCopy"
4806 co$(1)="Directory":co$(2)="Copy a File":co$(3)="Scratch File":co$(4)="Disk Menu"
4810 co=4:GOSUB26000
4815 ONaGOTO4825,4830,4870,4000
4820 GOTO4800
4825 PRINT"{clear}";:DIRECTORYu(dv%(dv)):PRINTds$:GETKEYa$:GOTO4800
4830 PRINT"{clear}This will destroy your buffer, Continue?";:GETKEYa$:IFa$<>"y"THENGOTO4800
4832 POKE250,0:POKE251,128:BANK1:POKE32768,0:BANK15
4835 PRINT"{clear}Filename:";:GOSUB3490:IFen$=""THEN4800:ELSEf$=en$
4836 PRINT"{clear}Filetype:";:GOSUB3490:IFen$=""THEN4800:ELSEft$=en$
4840 tt=0:OPEN1,8,2,f$+","+ft$+",r":IFds<>0THENDCLOSE:PRINT"Disk Error:"ds$:GETKEYa$:GOTO4800
4841 PRINT"{clear}Reading File{.*3}";
4845 BANK15:SYS3587:DCLOSE#1:DCLEAR
4850 BANK15:PRINT"{clear}Insert DESTINATION Disk and Press a Key.";:GETKEYa$:PRINT"{clear}Writing{.*3}";
4855 OPEN1,8,2,f$+","+ft$+",w":IFds<>0THENPRINT"{clear}Disk Error:"ds$:GETKEYa$:GOTO4800
4860 BANK15:SYS3707:DCLOSE#1:DCLEAR
4865 PRINT"{clear}Copy Complete{.*3}Press any Key.";:GETKEYa$:GOTO4800
4870 PRINT"{clear}Filename to Scratch:";:GOSUB3490:IFen$=""THEN4800:ELSEf$=en$
4875 SCRATCH(f$):PRINT"{clear}"ds$:GETKEYa$:GOTO4800
5000 mn$="Modem Parameters"
5020 co$(1)="Speaker:"+au$(sk%):co$(2)="Response:"+rp$(rp%)
5025 co$(3)="Pause:"+STR$(tt%/15):co$(4)="Auto Answer:"+au$(aa%):co$(5)="Echo:"+ee$(ee%)
5030 co$(6)="Parity:"+pa$(pa%):co$(7)="Word:"+wl$(wl%):co$(8)="Stop Bits:"+sb$(sb%):co$(9)="Handshake:"+hs$(hs%)
5085 co$(10)="Configure Menu"
5090 co=10:GOSUB26000
5092 ONaGOTO5100,5200,5300,5400,5150,5600,5650,5700,5750,3600
5095 GOTO5090
5100 sk%=-sk%+1:IFsk%=0THENPRINT#5,"atm0"+cr$
5105 IFsk%=1THENPRINT#5,"atm1"+cr$
5110 SLEEP1:GOTO5000
5150 ee%=-ee%+1:IFee%=1THENPRINT#5,"ate0"+cr$
5155 IFee%=0THENPRINT#5,"ate1"+cr$
5160 SLEEP1:GOTO5000
5200 rp%=-rp%+1:IFrp%=0THENPRINT#5,"atx1"+cr$:ELSEPRINT#5,"atx0"+cr$
5205 SLEEP1:GOTO5000
5300 PRINT"{clear}"cr$"Number Of Seconds"cr$"Before Disconnect:";:GOSUB3490:IFen$=""THENGOTO5000
5305 tt%=VAL(en$)*15:GOTO5000
5400 aa%=-aa%+1:IFaa%=0THENPRINT#5,"ats0=0"+cr$:ELSEPRINT#5,"ats0=1"+cr$
5405 SLEEP1:GOTO5000
5500 a$="at":IFsk%=1THENa$=a$+"m1"
5501 IFrp%=1THENa$=a$+"v0"
5502 IFaa%=1THENa$=a$+"s0=1"
5503 IFee%=1THENa$=a$+"e0"
5510 PRINT#5,a$cr$:SLEEP1:RETURN
5600 IFpa%=0THENpa%=1:GOTO5620
5601 IFpa%=1THENpa%=2:GOTO5620
5602 IFpa%=2THENpa%=3:GOTO5620
5603 IFpa%=3THENpa%=4:GOTO5620
5604 IFpa%=4THENpa%=0:GOTO5620
5620 GOSUB5800:GOTO5000
5650 IFwl%=0THENwl%=1:GOTO5620
5651 IFwl%=1THENwl%=2:GOTO5660
5652 IFwl%=2THENwl%=3:GOTO5660
5653 IFwl%=3THENwl%=0:GOTO5660
5660 GOSUB5800:GOTO5000
5700 sb%=-sb%+1:GOSUB5800:GOTO5000
5750 hs%=-hs%+1:GOSUB5800:GOTO5000
5800 REM "Command Register - 2576{space*4}Control Register - 2577"
5805 cr%=0:cn%=0:IFbr$="1200"THENcn%=8:ELSEcn%=6
5810 IFpa%=1THENcr%=cr%OR(2^5)
5811 IFpa%=2THENcr%=cr%OR(2^5)OR(2^6)
5812 IFpa%=3THENcr%=cr%OR(2^5)OR(2^7)
5813 IFpa%=4THENcr%=cr%OR(2^5)OR(2^7)OR(2^6)
5815 IFhs%=1THENcr%=cr%OR(2^0)
5820 POKE2577,cn%
5825 IFwl%=1THENcn%=cn%OR(2^5)
5826 IFwl%=2THENcn%=cn%OR(2^6)
5827 IFwl%=3THENcn%=cn%OR(2^6)OR(2^5)
5830 IFsb%=1THENcn%=cn%OR(2^7)
5835 POKE2576,cr%
5836 IFmo=2THENCLOSE5:OPEN5,2,0,CHR$(cn%)+CHR$(cr%)
5840 GOSUB5500:RETURN
6000 qu%=0:WINDOW1,1,78,20,1:CHAR1,34,0,"The Editor":WINDOW1,2,78,20:PRINTCHR$(27)"u";
6005 PRINT"{clear}Edit Buffer, File on disk, or New File (B/F/N)?";:GETKEYa$:PRINTa$
6010 IFa$="b"THEN6100
6020 IFa$="f"THEN6200
6030 IFa$="n"THEN6300
6040 GOTO6005
6100 PRINT"{clear}Transfering Buffer to Editor Memory{.*3}";:FORt=0TOed:ed$(t)="":NEXT
6105 ed=0:FORt=32768TO(PEEK(251)*256+PEEK(250)-1):BANK1:a$=CHR$(PEEK(t))
6110 IFa$=CHR$(13)ORLEN(ed$(ed))=79THENed$(ed)=ed$(ed)+CHR$(13):ed=ed+1:a$=""
6111 IFa$=CHR$(20)THENa$="":ed$(ed)=LEFT$(ed$(ed),LEN(ed$(ed))-1)
6112 ed$(ed)=ed$(ed)+a$
6113 IFed=100THENt=65535
6115 NEXT
6120 PRINT"{clear}Filename for Buffer:";:GOSUB3490:IFen$=""THENGOSUB1165:RETURN:ELSEf$=en$
6125 RETURN
6200 PRINT"{clear}Filename to Load:";:GOSUB3490:IFen$=""THENGOSUB1165:RETURN
6205 f$=en$:DOPEN#1,(f$):IFst<>0THENDCLOSE:PRINTds$:GETKEYa$:RETURN
6210 FORt=0TOed:ed$(ed)="":NEXT:ed=0:DO:GET#1,a$:s=st
6215 IFa$=CHR$(13)ORLEN(ed$(ed))=79THENed$(ed)=ed$(ed)+CHR$(13):ed=ed+1:a$=""
6220 IFa$=CHR$(20)THENa$="":ed$(ed)=LEFT$(ed$(ed),LEN(ed$(ed))-1)
6225 ed$(ed)=ed$(ed)+a$
6230 LOOPUNTILs<>0ORed=100::DCLOSE:GOTO6500
6300 PRINT"{clear}Filename for Disk:";:GOSUB3490:IFen$=""THENGOSUB1165:RETURN
6305 f$=en$:FORt=0TOed:ed$(t)="":NEXT:ed=0:GOTO6500
6500 ed%=1:PRINT"{clear}The Editor":PRINT"(ESC Exits to Editor Menu)":PRINT"("ed"lines in Workspace)"
6505 PRINT"{sh asterisk*78}"cr$;
6510 en$="":qu%=0
6515 GOSUB3495:IFtr%=1ANDqu%<>0THENGOSUB1165:PRINT"{clear}";:RETURN
6516 IFqu%<>0ANDtr%=0THENPRINT"{clear}";:RETURN
6517 IFed=100THENPRINT"{up}[ Editor Full ]"CHR$(27)"q":GOTO6510
6520 ed$(ed)=en$:ed=ed+1
6590 GOTO6510
6599 RETURN
6600 mn$="Editor Commands"
6610 co$(1)="Delete Line(s)"
6615 co$(2)="Edit Line"
6620 co$(3)="Read Workspace"
6625 co$(4)="List Workspace":co$(5)="Save FIle":co$(6)="Quit Editor"
6630 co=6:GOSUB26000
6635 IFa=1THENGOSUB6700:GOTO6600
6640 IFa=2THENGOSUB6800:GOTO6600
6645 IFa=3THENGOSUB6850
6650 IFa=4THENGOSUB6900
6655 IFa=6THENqu%=1:RETURN
6660 IFa=5THENGOSUB7400:GOTO6600
6699 PRINTcr$"Editor:":RETURN
6700 PRINT"{clear}Delete:":PRINT"[R] Range":PRINT"[S] Single Line":GETKEYa$
6705 IFa$="r"THENBEGIN:PRINT"{clear}Start:";:ed%=0:GOSUB3490:ed%=1:IFen$=""ORVAL(en$)<0ORVAL(en$)>edTHEN6600:ELSEs=VAL(en$)
6710 :PRINT"{space*2}End:";:ed%=0:GOSUB3490:ed%=1:IFen$=""ORVAL(en$)<0ORVAL(en$)>edTHEN6600:ELSEe=VAL(en$)
6715 :tt=s:FORg=1TOe-s+1:GOSUB7300:NEXT:BEND
6720 IFa$="s"THENBEGIN:PRINT"{clear}Line:";:ed%=0:GOSUB3490:ed%=1:IFen$=""ORVAL(en$)<0ORVAL(en$)>edTHEN6600:ELSEtt=VAL(en$):GOSUB7300:BEND
6725 RETURN
6800 PRINT"{clear}Line to Edit:";:ed%=0:GOSUB3490:ed%=1:IFen$=""ORVAL(en$)<0ORVAL(en$)>edTHENRETURN
6805 l=VAL(en$)
6810 PRINT"Edit:"cr$ed$(l):GOSUB3490:IFen$=""THENPRINT"(No Change)":SLEEP1:RETURN
6815 ed$(l)=en$:RETURN
6850 PRINT"{clear}Read":PRINT"[R] Range":PRINT"[A] All Lines":GETKEYa$
6855 IFa$="r"THENBEGIN:PRINT"{clear}Starting Line:";:ed%=0:GOSUB3490:ed%=1:IFen$=""ORVAL(en$)=0ORVAL(en$)>edTHENRETURN:ELSEs=VAL(en$)
6860 :PRINT"{space*2}Ending Line:";:ed%=0:GOSUB3490:ed%=1:IF en$="" OR VAL(en$)<0 OR VAL(en$)>ed OR VAL(en$)<s THEN RETURN:ELSE:e=VAL(en$)
6865 :PRINT"{clear}";:FORt=sTOe:PRINTed$(t)
6866 :GETa$:IFa$=" "THENt=ed
6867 :NEXT:RETURN:BEND
6870 IFa$="a"THENBEGIN:PRINT"{clear}";:FORt=0TOed-1:PRINTed$(t)
6875 :GETa$:IFa$=" "THENt=ed
6880 :NEXT:RETURN:BEND
6885 RETURN
6900 PRINT"{clear}List":PRINT"[R] Range":PRINT"[A] All Lines":GETKEYa$
6905 IFa$="r"THENBEGIN:PRINT"{clear}Starting Line:";:ed%=0:GOSUB3490:ed%=1:IFen$=""ORVAL(en$)<0ORVAL(en$)>edTHENRETURN:ELSEs=VAL(en$)
6910 :PRINT"{space*2}Ending Line:";:ed%=0:GOSUB3490:ed%=1:IFen$="" OR VAL(en$)<0 ORVAL(en$)>ed OR VAL(en$)<s THENRETURN:ELSE:e=VAL(en$)
6915 :PRINT"{clear}";:FORt=sTOe:PRINTt"{left}:"cr$ed$(t)
6920 :GETa$:IFa$=" "THENt=ed
6925 :NEXT:RETURN:BEND
6930 IFa$="a"THENBEGIN:PRINT"{clear}";:FORt=0TOed-1:PRINTt"{left}:"cr$ed$(t)
6935 :GETa$:IFa$=" "THENt=ed
6940 :NEXT:RETURN:BEND
6945 RETURN
7000 IFa$="{f1}"ORa$="{f3}"ORa$="{f5}"ORa$="{f7}"ORa$="{f2}"ORa$="{f4}"ORa$="{f6}"ORa$="{f8}"THENa$=""
7005 IFa$="{right}"ORa$="{left}"ORa$="{up}"ORa$="{down}"ORa$="{home}"ORa$="{clear}"THENa$=""
7010 IFASC(a$)<32ANDa$<>CHR$(13)ANDa$<>CHR$(20)THENa$=""
7050 RETURN
7100 POKEa,12:POKEa+1,0:POKEa,13:POKEa+1,0:POKE2606,0:CHAR1,x+1,y-3,"":RETURN
7105 a=54784:BANK15:POKEa,12:POKEa+1,16:POKEa,13:POKEa+1,0:POKE2606,16:x=PEEK(236):y=PEEK(235)
7200 a=54784:BANK15:POKEa,12:POKEa+1,16:POKEa,13:POKEa+1,0:POKE2606,16:x=PEEK(236):y=PEEK(235):RETURN
7300 PRINT"Working{.*3}";:ed$(tt)="":FORt=tt+1TOed:ed$(t-1)=ed$(t):NEXT:ed=ed-1:PRINTCHR$(27)"p"cr$"{up}";:IFed=-1THENed=0:RETURN:ELSERETURN
7400 PRINT"{clear}Are you Sure? ";:GETKEYa$:PRINTa$;:IFa$<>"y"THENRETURN
7405 PRINTCHR$(27)"p"CHR$(13)"{up}Saving{.*3}";
7410 SCRATCH(f$):DOPEN#1,(f$+",s,w"):IFst<>0THENPRINT"Error:"ds$:DCLOSE:GETKEYa$:RETURN
7415 FORt=0TOed-1:PRINT#1,ed$(t):NEXT:DCLOSE:PRINT"{clear}Saved.";:GETKEYa$:RETURN
7450 RETURN
8000 BANK15:ad=2000+y*80+x:POKE 250,(adAND255):POKE251,(ad/256):a=(t$):SYSDEC("1627"),aAND255,a/256:RETURN
9000 mn$="Multi-Xfer":co$(1)="Multi-Upload":co$(2)="Multi-Download":co$(3)="Send Disk":co$(4)="Main Menu":co=4:GOSUB26000
9005 ONaGOTO2064,2142,2034,9010:GOTO9000
9010 RETURN
10000 WINDOW1,2,78,20,1
10001 PRINT"{clear}Old Filename:";:GOSUB3490:f1$=en$:IFen$=""THENGOTO4000
10005 PRINT"New Filename:";:GOSUB3490:f2$=en$:IFen$=""THENGOTO4000
10010 OPEN1,8,2,"0:"+f1$+",s,r"
10015 OPEN2,8,12,"0:"+f2$+",s,w"
10020 GET#1,a$:s=st
10025 a=ASC(a$)
10030 IFa$=CHR$(13)THENa=13:GOTO10050
10031 IFa=10THENa=0:GOTO10050
10035 IFa$=" "THENa=32:GOTO10050
10040 IFa>96THENa=a-32:GOTO10050
10045 IFa>64ANDa<90THENa=a+32
10050 a$=CHR$(a):PRINTa$;:PRINT#2,a$;
10055 IFs<>0THENDCLOSE:GOTO4000
10060 GOTO10020
26000 REM "{space*3}Master Menus(tm) (C) 1987 PC-Soft{space*2}"
26001 REM "Master Menus is a Trademark of PC-Soft"
26002 REM "{space*8}Written by Sam Pullara{space*8}"
26003 REM "{space*38}"
26004 REM "Variables Used:{space*23}"
26005 REM "a$,a,aa,x,co,co$(xx),dn$,gg,sp${space*7}"
26007 REM "Input Variables:{space*22}"
26008 REM "co$(xx){space*5}--{space*4}Menu Options{space*8}"
26009 REM "co{space*10}--{space*4}# of Menu Options{space*3}"
26010 REM "tb%{space*9}--{space*4}Menu Column 0-77{space*4}"
26011 REM "Output Variables>{space*21}"
26012 REM "a{space*11}--{space*4}Menu Option chosen{space*2}"
26013 REM "{space*18}by User{space*13}"
26014 tx$=mn$:GOSUB1335
26070 WINDOW1,1,78,20,1
26075 PRINT"{home}"TAB(tb%+((24-LEN(mn$)))/2)mn$
26090 WINDOW1,2,78,20,1
26100 dn$="{down*22}":a=1:md=0:sp$="{space*19}":PRINTCHR$(14)+"{clear}"TAB(tb%)"{cm a}{sh asterisk*22}{cm s}"
26110 FORgg=1TOco:PRINTTAB(tb%)"{sh -}{space*2}"+CHR$(96+gg)+". "+co$(gg)+LEFT$(sp$,16-LEN(co$(gg)))+" {sh -}":NEXT
26120 PRINTTAB(tb%)"{cm z}{sh asterisk*22}{cm x}{home}"TAB(tb%)"{down}{right}>{left}";
26130 j2=JOY(2):GETa$:IFj1=0ANDj2=0ANDa$=""THEN26130
26131 IFj1=128ORj2=128THENa$=CHR$(13)
26135 IFa$=CHR$(27)THENGOSUB4700:GOTO26070
26140 aa=ASC(a$)-64:IFaa>0ANDaa<co+1THENa=aa:PRINT"{clear}";:RETURN
26150 IFa$<>"{down}"ANDa$<>"{up}"ANDa$<>CHR$(13)ANDj2=0THEN26130
26160 IFa$=CHR$(13)THENPRINT"{clear}":RETURN
26170 IFj2=5ORa$="{down}"THENa=a+1
26180 IFj2=1ORa$="{up}"THENa=a-1
26190 IFa>coTHENa=1:PRINT" {home}"TAB(tb%)"{down}{right}>{left}";:GOTO26130
26200 IFa<1THENPRINTTAB(29)" "+"{home}"TAB(tb%)LEFT$(dn$,co)+"{right}>{left}";:a=co:GOTO26130
26210 IFa$<>""ORj2<>0THENPRINTTAB(tb%)" "a$"{left}>{left}";
26220 GOTO26130
40000 ' s{117}{098}{114}{111}{117}{116}{105}{110}{101} {100}{101}{102}{105}{110}{105}{110}{103} vt-102 {115}{099}{114}{101}{101}{110} {104}{097}{110}{100}{108}{105}{110}{103} {118}{097}{114}{105}{097}{098}{108}{101}{115} & {102}{117}{110}{099}{116}{105}{111}{110}{115}
